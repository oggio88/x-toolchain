# Contributor: ant32 <antreimer@gmail.com>

pkgname="${_target}-curl"
_basename="${pkgname#${_target}-}"
pkgver=7.70.0
pkgrel=1
pkgdesc="An URL retrival utility and library (${_target})"
arch=('any')
url="http://curl.haxx.se"
license=("MIT")

if [[ ${_os} == *"mingw"* ]]
then
  depends=("${_target}-gcc"
          "${_target}-libidn2"
          "${_target}-libssh2"
          "${_target}-openssl"
          "${_target}-zlib")
  _idn_configure_flag="--with-libidn2"
else
  depends=("${_target}-gcc"
          "${_target}-libssh2"
          "${_target}-openssl"
          "${_target}-zlib")
  _idn_configure_flag=""
fi

makedepends=("${_target}-configure")
options=('staticlibs' '!strip' '!buildflags')
source=("https://curl.haxx.se/download/$_basename-$pkgver.tar.gz"{,.asc})
sha512sums=('76f073f8e2b18326d8741774927e99bad038b02fdb4446f2fa8c1bc1cd8909ec7e666d4bac67e7ee87a80ba7010fcf21750fa44bc13edcfbab8a287cb2e3660e'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') #

build() {
  cd "${srcdir}"/${pkgname#${_target}-}-${pkgver}
    configure_args="--with-ssl \
                    --disable-ldap \
                    --disable-ldaps \
                    --disable-manual \
                    --enable-ipv6 \
                    --enable-threaded-resolver \
                    ${_idn_configure_flag} \
                    --with-libssh2 \
                    --enable-versioned-symbols \
                    --with-gssapi \
                    --without-ca-bundle \
                    --without-random"
    mkdir -p build-${_target}-static && pushd build-${_target}-static
    ${_target}-configure $configure_args \
      --enable-static --disable-shared ..
    make
    popd
    mkdir -p build-${_target} && pushd build-${_target}
    ${_target}-configure $configure_args \
      --disable-static --enable-shared ..
    make
    popd
}

package() {
    cd "${srcdir}/${pkgname#${_target}-}-${pkgver}/build-${_target}-static"
    make DESTDIR="${pkgdir}" install
    cd "${srcdir}/${pkgname#${_target}-}-${pkgver}/build-${_target}"
    make DESTDIR="${pkgdir}" install
    find "$pkgdir/${_prefix}/${_target}" -name '*.a' -name '*.so' -type f -exec ${_target}-strip -g {} \;
    sed -i "s/-lidn2/-lidn2 -lunistring/g" "$pkgdir/${_prefix}/${_target}/sysroot/lib/pkgconfig/libcurl.pc"
}
