# $Id$
# Maintainer: Sébastien Luttringer <seblu@seblu.net>

pkgname="${_target}-wayland-protocols"
_basename="${pkgname#${_target}-}"
pkgver=1.13
pkgrel=1
pkgdesc='Specifications of extended Wayland protocols'
arch=('any')
url='https://wayland.freedesktop.org/'
license=('MIT')
options=('staticlibs' '!strip' '!buildflags')
makedepends=('wayland')
validpgpkeys=('8307C0A224BABDA1BABD0EB9A6EEEC9E0136164A'  # Jonas Ådahl
              'A66D805F7C9329B4C5D82767CCC4F07FAC641EFF') # Daniel Stone
source=("https://wayland.freedesktop.org/releases/$_basename-$pkgver.tar.xz"{,.sig})
sha1sums=('1b4b3385e168c361b4d350668d92f3a90ceee189'
          'SKIP')

build() {
  cd $_basename-$pkgver

  ${_target}-configure .
  make
}

package() {
  cd $_basename-$pkgver

  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/${_prefix}/${_target}/sysroot/lib"
  mv "$pkgdir/${_prefix}/${_target}/sysroot/share/pkgconfig" "$pkgdir/${_prefix}/${_target}/sysroot/lib"
  install -Dm 644 COPYING "$pkgdir/${_prefix}/${_target}/sysroot/share/licenses/$_basename/COPYING"
}

# vim:set ts=2 sw=2 et:
