# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname="${_target}-sqlite"
_srcver=3310100
_docver=${_srcver}
#_docver=3250200
pkgver=3.31.1
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('x86_64')
license=('custom:Public Domain')
url="http://www.sqlite.org/"
depends=("${_target}-gcc" "${_target}-readline")
makedepends=("${_target}-readline" "${_target}-configure")
source=("https://www.sqlite.org/2020/sqlite-autoconf-${_srcver}.tar.gz")
sha256sums=('62284efebc05a76f909c580ffa5c008a7d22a1287285d68b7825a2b6b51949ae')
options=('!emptydirs' '!makeflags' 'staticlibs' '!buildflags') # json extensions breaks parallel build

prepare() {
  pushd "${srcdir}/sqlite-autoconf-${_srcver}"
  autoreconf -vfi
  mkdir -p build-${_target}
  popd
}

build() {
  export CPPFLAGS="$CPPFLAGS -DSQLITE_ENABLE_COLUMN_METADATA=1 \
                             -DSQLITE_ENABLE_UNLOCK_NOTIFY \
                             -DSQLITE_ENABLE_DBSTAT_VTAB=1 \
                             -DSQLITE_ENABLE_FTS3_TOKENIZER=1 \
                             -DSQLITE_SECURE_DELETE \
                             -DSQLITE_MAX_VARIABLE_NUMBER=250000 \
                             -DSQLITE_MAX_EXPR_DEPTH=10000"

  # build sqlite
  pushd "${srcdir}/sqlite-autoconf-${_srcver}/build-${_target}"
  ${_target}-configure \
    --disable-editline \
    --enable-readline \
    --enable-fts3 \
    --enable-fts4 \
    --enable-fts5 \
    --enable-rtree \
    --enable-json1 \
    --enable-fts5 \
    --enable-session \
    .
  make
  popd
}

package() {
  pkgdesc="A C library that implements an SQL database engine"
  provides=("${_target}-sqlite3=$pkgver")
  replaces=("${_target}-sqlite3")

  pushd "${srcdir}/sqlite-autoconf-${_srcver}/build-${_target}"
  make DESTDIR=${pkgdir} install
  popd
}

strip() {
    ${_target}-strip $@
}
export -f strip
