# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
pkgname="${_target}-libpng"
pkgver=1.6.36
_basename="${pkgname#${_target}-}"
_apngver=${pkgver}
pkgrel=2
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=("${_target}-gcc" "${_target}-zlib" 'sh')
makedepends=("${_target}-configure")
options=('staticlibs' '!strip' '!buildflags')
validpgpkeys=('8048643BA2C840F4F92A195FF54984BFA16C640F') # Glenn Randers-Pehrson (mozilla) <glennrp@gmail.com>
source=("https://netix.dl.sourceforge.net/project/$_basename/libpng16/$pkgver/$_basename-$pkgver.tar.xz"
        "https://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-$_apngver-apng.patch.gz")
sha256sums=('eceb924c1fa6b79172fdfd008d335f0e59172a86a66481e09d4089df872aa319'
            '8c1f33da739b29a77e35e5a1ebcdffaab63bfe184be8b5a97677cc80156fdc0f')

prepare() {
  cd "${_basename}-$pkgver"

  # Add animated PNG (apng) support. Required by Firefox
  # see http://sourceforge.net/projects/libpng-apng/
  patch -Np1 -i ../libpng-$_apngver-apng.patch
}

build() {
  cd "${_basename}-$pkgver"
  mkdir -p build-${_target} && pushd build-${_target}
  ${_target}-configure
  make
  popd
}

package() {
  cd "${_basename}-$pkgver/build-${_target}"
  make DESTDIR="$pkgdir" install
  find "$pkgdir/${_prefix}/${_target}" -name '*.a' -name '*.so' -type f -exec ${_target}-strip -g {} \;
}