# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname="${_target}-xtrans"
_basename="${pkgname#${_target}-}"
pkgver=1.3.5
pkgrel=1
pkgdesc="X transport library"
arch=('any')
license=('custom')
url="https://xorg.freedesktop.org/"
options=(staticlibs !buildflags !emptydirs)
makedepends=("${_target}-configure")
source=(${url}/releases/individual/lib/${_basename}-${pkgver}.tar.bz2{,.sig})
sha256sums=('adbd3b36932ce4c062cd10f57d78a156ba98d618bdb6f50664da327502bc8301'
            'SKIP')
validpgpkeys=('C383B778255613DFDB409D91DB221A6900000011') #  "Keith Packard <keithp@keithp.com>"

build() {
  cd ${_basename}-${pkgver}
  ${_target}-configure .
}

package() {
  cd ${_basename}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/${_prefix}/${_target}/sysroot/lib"
  mv "${pkgdir}/${_prefix}/${_target}/sysroot/share/pkgconfig" "${pkgdir}/${_prefix}/${_target}/sysroot/lib"
}

strip() {
    ${_target}-strip $@
}
export -f strip