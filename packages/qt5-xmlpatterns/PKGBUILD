# $Id: PKGBUILD 240297 2015-06-03 10:22:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname="${_target}-qt5-xmlpatterns"
_basename="${pkgname#${_target}-}"
_qtver=5.12.0
pkgver=${_qtver/-/}
pkgrel=1
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
options=('staticlibs' '!strip' '!buildflags')
pkgdesc='Support for XPath, XQuery, XSLT and XML schema validation'
depends=("${_target}-qt5-base")
makedepends=()
conflicts=("${_target}-qtchooser")
groups=("${_target}-qt" "${_target}-qt5")
_pkgfqn="${_basename/5-/}-everywhere-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('f36279bcafb8052a0b60edd473e1d341b4733a43eb26239848b4382303679375')

prepare() {
  mkdir -p build
}

build() {
  cd build
  ${_prefix}/bin/${_target}-qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  find "$pkgdir/${_prefix}/${_target}" -name '*.a' -name '*.so' -type f -exec ${_target}-strip -g {} \;
  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/${_prefix}/${_target}/sysroot/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  #install -d "$pkgdir"/usr/share/licenses
  #ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
