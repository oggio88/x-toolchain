# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname="${_target}-libgpg-error"
_basename="${pkgname#${_target}-}"
pkgver=1.37
pkgrel=1
pkgdesc="Support library for libgcrypt"
arch=(x86_64)
url="https://www.gnupg.org"
license=('LGPL')
options=(staticlibs !buildflags)
depends=("${_target}-gcc" 'sh')
makedepends=("${_target}-configure")
source=(https://www.gnupg.org/ftp/gcrypt/libgpg-error/${_basename}-${pkgver}.tar.bz2{,.sig})
# https://www.gnupg.org/download/integrity_check.html
sha1sums=('6dff83371e0c03fe9ba468cc23d528a8c247785a'
          'SKIP')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'  # Werner Koch
              '031EC2536E580D8EA286A9F22071B08A33BD3F06') # NIIBE Yutaka (GnuPG Release Key) <gniibe@fsij.org>
build() {
  cd ${_basename}-${pkgver}

  if [[ $_os == linux* ]]
  then
    if [[ $_cpu == x86_64 ]] && [[ ${_os} == "linux-musl" ]]
    then
      cp src/syscfg/lock-obj-pub.x86_64-unknown-linux-musl.h src/syscfg/lock-obj-pub.${_cpu}-${_vendor}-${_os}.h
    elif [[ $_cpu == arm ]] && [[ ${_os} == linux-musl* ]]
    then
      cp src/syscfg/lock-obj-pub.arm-unknown-linux-gnueabi.h src/syscfg/lock-obj-pub.${_os}.h
    fi
  fi

  ./configure --build="$CHOST" --host="${_cpu}-${_vendor}-${_os}" --target="${_cpu}-${_vendor}-${_os}" \
    --prefix=${_prefix}/${_target} --libdir=${_prefix}/${_target}/sysroot/lib --includedir=${_prefix}/${_target}/sysroot/include \
    --enable-shared --enable-static
  make
}

package() {
  cd ${_basename}-${pkgver}
  make DESTDIR="${pkgdir}/" install
}

strip() {
    ${_target}-strip $@
}
export -f strip