# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: SÃ©bastien Luttringer
# Contributor: Lawrence Lee <valheru@facticius.net>
# Contributor: Phillip Marvin <phillip.marvin@gmail.com>
# Contributor: keystone <phillip.marvin@gmail.com>

pkgname="${_target}-libunwind"
_basename="${pkgname#${_target}-}"
pkgver=1.2.1
pkgrel=1
pkgdesc='Portable and efficient C programming interface (API) to determine the call-chain of a program'
arch=('x86_64')
url='http://www.nongnu.org/libunwind/'
license=('GPL')
options=(!strip !buildflags staticlibs)
depends=("${_target}-gcc" "${_target}-configure")
makedepends=('texlive-core')
source=("http://download.savannah.nongnu.org/releases/$_basename/$_basename-$pkgver.tar.gz"{,.sig})
sha512sums=('af7c280d2a963779a4a2711887618bc96383011e4e5d52e4085aa7fb351e55e357468f6ff85e66a216f1c6826538f498335a917a5970575c93be74c96316319b'
            'SKIP')
validpgpkeys=('5C96BDEAF5F47FB02BD4F6B965D98560914F3F48'  # Arun Sharma
              '1675C8DA2EF907FB116EB709EC52B396E6874AF2') # Dave Watson

prepare () {
  cd $_basename-$pkgver
  autoconf
}

build() {
  cd $_basename-$pkgver 
  export LIBS="-lexecinfo" 
  ${_target}-configure --enable-cxx-exceptions .
  make
}

package() {
  cd $_basename-$pkgver
  make DESTDIR="$pkgdir" install
  find "$pkgdir/${_prefix}/${_target}" -name '*.a' -name '*.so' -type f -exec ${_target}-strip -g {} \;
}

# vim:set ts=2 sw=2 et:
