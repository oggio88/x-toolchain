# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname="${_target}-xcb-proto"
_basename="${pkgname#${_target}-}"
pkgver=1.13
pkgrel=1
pkgdesc="XML-XCB protocol descriptions"
arch=(any)
url="https://xcb.freedesktop.org/"
license=('custom')
options=(staticlibs !strip !buildflags)
makedepends=("${_target}-python" "${_target}-libxml2")
source=($url/dist/${_basename}-$pkgver.tar.bz2{,.sig})
sha512sums=('002aa2aa0ad503977e03419c6f136f1e3aa8c787916830ce6d6be8f81ca99edc1d8fc5f71ce8c592b490cc767bf6567695f0bafe55fe3f743076bcddbaac07f7'
            'SKIP')
validpgpkeys=('A66D805F7C9329B4C5D82767CCC4F07FAC641EFF') # "Daniel Stone <daniels@collabora.com>"

build() {
  cd $_basename-$pkgver
  ${_target}-configure .
  make
}

package() {
  cd $_basename-$pkgver
  make DESTDIR="$pkgdir" install
  find "$pkgdir/${_prefix}/${_target}" -name '*.a' -name '*.so' -type f -exec ${_target}-strip -g {} \;
}
