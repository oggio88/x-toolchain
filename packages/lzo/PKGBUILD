# $Id$
# Contributor: Low Kian Seong <fastmail_low@speedymail.org>
# Maintainer: dorphell <dorphell@archlinux.org>

pkgname="${_target}-lzo"
_basename="${pkgname#${_target}-}"
pkgver=2.10
pkgrel=1
pkgdesc="Portable lossless data compression library"
arch=('x86_64')
url="http://www.oberhumer.com/opensource/lzo"
license=('GPL')
options=(!buildflags staticlibs)
depends=("${_target}-gcc")
makedepends=("${_target}-configure")
source=(http://www.oberhumer.com/opensource/lzo/download/lzo-${pkgver}.tar.gz)
sha1sums=('4924676a9bae5db58ef129dc1cebce3baa3c4b5d')

build() {
  cd lzo-${pkgver}
  ${_target}-configure .
  make

  # build minilzo
  ${_target}-gcc $CFLAGS -fpic -Iinclude/lzo -o minilzo/minilzo.o -c minilzo/minilzo.c
  ${_target}-gcc $LDFLAGS -shared -o libminilzo.so.0 -Wl,-soname,libminilzo.so.0 minilzo/minilzo.o
}

package() {
  cd lzo-${pkgver}
  make DESTDIR=${pkgdir} install

  # install minilzo
  install -m 755 libminilzo.so.0 "${pkgdir}/${_prefix}/${_target}/sysroot/usr/lib"
  install -p -m 644 minilzo/minilzo.h "${pkgdir}/${_prefix}/${_target}/sysroot/usr/include/lzo"
  cd ${pkgdir}/${_prefix}/${_target}/sysroot/usr/lib
  ln -s libminilzo.so.0 libminilzo.so
}

strip() {
    ${_target}-strip $@
}
export -f strip