# Maintainer: pingplug <pingplug@foxmail.com>
# Contributor: Filip Brcic <brcha@gna.org>

# Thanks to the patch from the MXE project <mxe.cc>

pkgname=${_target}-readline
_basever=7.0
_patchlevel=005
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc="Library for editing typed command lines (${_target})"
arch=('any')
depends=("${_target}-gcc" "${_target}-ncurses")
makedepends=("${_target}-configure")
options=('staticlibs' '!buildflags' '!strip')
license=('GPL')
url="https://tiswww.case.edu/php/chet/readline/rltop.html"
source=("https://ftp.gnu.org/gnu/readline/readline-${_basever}.tar.gz"{,.sig})

if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} https://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//./}-$(printf "%03d" $p){,.sig})
    done
fi

sha256sums=('750d437185286f40a369e1e4f4764eda932b9459b5ec9a731628393dd3d32334'
            'SKIP'
            '9ac1b3ac2ec7b1bf0709af047f2d7d2a34ccde353684e57c6b47ebca77d7a376'
            'SKIP'
            '8747c92c35d5db32eae99af66f17b384abaca961653e185677f9c9a571ed2d58'
            'SKIP'
            '9e43aa93378c7e9f7001d8174b1beb948deefa6799b6f581673f465b7d9d4780'
            'SKIP'
            'f925683429f20973c552bff6702c74c58c2a38ff6e5cf305a8e847119c5a6b64'
            'SKIP'
            'ca159c83706541c6bbe39129a33d63bbd76ac594303f67e4d35678711c51b753'
            'SKIP')

validpgpkeys=(7C0135FB088AAF6C66C650B9BB5869F064EA74AB)

prepare() {
  cd "${srcdir}/readline-${_basever}"

  for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
    msg "applying patch readline${_basever//./}-$(printf "%03d" $p)"
    patch -Np0 -i $srcdir/readline${_basever//./}-$(printf "%03d" $p)
  done

  # Remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf
}

build() {
  cd "${srcdir}/readline-${_basever}"
    unset LDFLAGS
    mkdir -p build-${_target} && pushd build-${_target}
    bash_cv_wcwidth_broken=no ${_target}-configure \
      --target=${_target} \
      --enable-multibyte \
      --without-purify \
      --with-curses
    make SHLIB_LIBS="-lncurses"
    popd
}

package() {
    cd "${srcdir}/readline-${_basever}/build-${_target}"
    make install DESTDIR="${pkgdir}" SHLIB_LIBS="-lncurses"
    find "$pkgdir/${_prefix}/${_target}" -name '*.a' -name '*.so' -type f -exec ${_target}-strip -g {} \;
    rm "$pkgdir/${_prefix}/${_target}/sysroot/share/info/dir"
}

# vim:set ts=2 sw=2 et:
