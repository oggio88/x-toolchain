# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=${_target}-bash
_basename="${pkgname#${_target}-}"
_basever=5.0
_patchlevel=016
pkgver=${_basever}.${_patchlevel}
pkgrel=1
pkgdesc='The GNU Bourne Again shell'
arch=(x86_64)
license=(GPL)
options=(staticlibs !strip !buildflags)
url='http://www.gnu.org/software/bash/bash.html'
groups=(base)
backup=("./${_prefix}/${_target}/sysroot/etc/"bash.bash{rc,_logout} "./${_prefix}/${_target}/sysroot/etc/skel/".bash{rc,_profile,_logout})
depends=("${_target}-readline>=7.0" "${_target}-gcc" "${_target}-ncurses")
makedepends=("${_target}-configure")
optdepends=("${_target}-bash-completion: for tab completion")
provides=("${_target}-sh")
source=(https://ftp.gnu.org/gnu/bash/bash-$_basever.tar.gz{,.sig}
        dot.bashrc
        dot.bash_profile
        dot.bash_logout
        system.bashrc
        system.bash_logout)
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB') # Chet Ramey

if [[ $((10#${_patchlevel})) -gt 0 ]]; then
  for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
    source=(${source[@]} https://ftp.gnu.org/gnu/bash/bash-$_basever-patches/bash${_basever//.}-$(printf "%03d" $_p){,.sig})
  done
fi

prepare() {
  cd $_basename-$_basever

  for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
    msg "applying patch bash${_basever//.}-$(printf "%03d" $_p)"
    patch -p0 -i ../bash${_basever//.}-$(printf "%03d" $_p)
  done
}

build() {
  cd $_basename-$_basever

  _bashconfig=(-DDEFAULT_PATH_VALUE=\'\"/usr/local/sbin:/usr/local/bin:/usr/bin\"\'
               -DSTANDARD_UTILS_PATH=\'\"/usr/bin\"\'
               -DSYS_BASHRC=\'\"/etc/bash.bashrc\"\'
               -DSYS_BASH_LOGOUT=\'\"/etc/bash.bash_logout\"\'
               -DNON_INTERACTIVE_LOGIN_SHELLS)
  export CFLAGS="${CFLAGS} ${_bashconfig[@]}"

  ${_target}-configure --with-curses --enable-readline \
    --without-bash-malloc --with-installed-readline .
  make
}

package() {
  make -C $_basename-$_basever DESTDIR="$pkgdir" install
  ln -s bash "$pkgdir/${_prefix}/${_target}/sysroot/bin/sh"

  # system-wide configuration files
  install -Dm644 system.bashrc "$pkgdir/${_prefix}/${_target}/sysroot/etc/bash.bashrc"
  install -Dm644 system.bash_logout "$pkgdir/${_prefix}/${_target}/sysroot/etc/bash.bash_logout"

  # user configuration file skeletons
  install -dm755 "$pkgdir/${_prefix}/${_target}/sysroot/etc/skel/"
  install -m644 dot.bashrc "$pkgdir/${_prefix}/${_target}/sysroot/etc/skel/.bashrc"
  install -m644 dot.bash_profile "$pkgdir/${_prefix}/${_target}/sysroot/etc/skel/.bash_profile"
  install -m644 dot.bash_logout "$pkgdir/${_prefix}/${_target}/sysroot/etc/skel/.bash_logout"
  find "$pkgdir/${_prefix}/${_target}/sysroot/"{bin,lib} -name '*.a' -name '*.so' -type f -exec ${_arch}-strip -g {} \;
  rm "$pkgdir/${_prefix}/${_target}/sysroot/share/info/dir"
}

md5sums=('2b44b47b905be16f45709648f671820b'
         'SKIP'
         '027d6bd8f5f6a06b75bb7698cb478089'
         '2902e0fee7a9168f3a4fd2ccd60ff047'
         '42f4400ed2314bd7519c020d0187edc5'
         'd8f3f334e72c0e30032eae1a1229aef1'
         '472f536d7c9e8250dc4568ec4cfaf294'
         'b026862ab596a5883bb4f0d1077a3819'
         'SKIP'
         '2f4a7787365790ae57f36b311701ea7e'
         'SKIP'
         'af7f2dd93fd5429fb5e9a642ff74f87d'
         'SKIP'
         'b60545b273bfa4e00a760f2c648bed9c'
         'SKIP'
         '875a0bedf48b74e453e3997c84b5d8a4'
         'SKIP'
         '4a8ee95adb72c3aba03d9e8c9f96ece6'
         'SKIP'
         '411560d81fde2dc5b17b83c3f3b58c6f'
         'SKIP'
         'dd7cf7a784d1838822cad8d419315991'
         'SKIP'
         'c1b3e937cd6dccbb7fd772f32812a0da'
         'SKIP'
         '19b41e73b03602d0e261c471b53e670c'
         'SKIP'
         '414339330a3634137081a97f2c8615a8'
         'SKIP'
         '1870268f62b907221b078ad109e1fa94'
         'SKIP'
         '40d923af4b952b01983ed4c889ae2653'
         'SKIP'
         '57857b22053c8167677e5e5ac5c6669b'
         'SKIP'
         'c4c6ea23d09a74eaa9385438e48fdf02'
         'SKIP'
         'a682ed6fa2c2e7a7c3ba6bdeada07fb5'
         'SKIP')
