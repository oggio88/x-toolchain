# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: John Proctor <jproctor@prium.net>

pkgname="${_target}-python"
_basename="${pkgname#${_target}-}"
pkgver=3.7.1
pkgrel=1
pkgdesc="Next generation of the python high-level scripting language"
url="http://www.python.org/"
arch=('x86_64')
license=('custom')
options=(staticlibs !strip !buildflags)
depends=("${_target}-gcc"
         "${_target}-libffi"
         "${_target}-expat" 
         "${_target}-zlib" 
         "${_target}-bzip2" 
         "${_target}-openssl"
         )
makedepends=("cmake" "ninja" "ccache" git "${_target}-configure")
source=(
    "https://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tgz"
    "config.site"
    "dont-make-libpython-readonly.patch"
    "0001-compileall-Fix-ddir-when-recursing.patch"
)
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')
provides=("${_target}-python3")
replaces=("${_target}-python3")

prepare() {
  cd Python-${pkgver}

  # FS#45809
  patch -p1 -i ../dont-make-libpython-readonly.patch

  # FS#59997
  patch -p1 -i ../0001-compileall-Fix-ddir-when-recursing.patch

  # https://bugs.python.org/issue34587
  sed -i -e "s|testCongestion|disabled_&|" Lib/test/test_socket.py

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

  # Speed up LTO
  sed -i -e "s|-flto |-flto=4 |g" configure configure.ac

  # Ensure that we are using the system copy of various libraries (expat, libffi, and libmpdec),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/_ctypes/{darwin,libffi}*
  rm -r Modules/_decimal/libmpdec
}

build() {
  mkdir build
  pushd build
  export PKG_CONFIG_LIBDIR="${_prefix}/${_target}/sysroot/lib/pkgconfig"
  CONFIG_SITE="${srcdir}/config.site" ${_target}-configure \
              --enable-shared \
              --with-threads \
              --with-computed-gotos \
              --enable-optimizations \
              --with-lto \
              --enable-ipv6 \
              --with-system-expat \
              --with-dbmliborder=gdbm:ndbm \
              --with-system-ffi \
              --with-system-libmpdec \
              --enable-loadable-sqlite-extensions \
              --without-ensurepip \
              "../Python-${pkgver}"
    make
  popd
}

package() {
  pushd build
  make install DESTDIR="${pkgdir}"
  find "$pkgdir/${_prefix}/${_target}" -name '*.a' -name '*.so' -type f -exec ${_target}-strip -g {} \;
  popd
}
