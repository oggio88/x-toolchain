# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname="${_target}-openssl"
_basename="${pkgname#${_target}-}"
_ver=1.1.1g
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
#pkgver=$_ver
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
options=(!strip !buildflags staticlibs)
depends=('perl' "${_target}-zlib" "${_target}-gcc")
makedepends=("${_target}-configure")
optdepends=("${_target}-ca-certificates")
backup=("./${_prefix}/${_target}/sysroot/etc/ssl/openssl.cnf")
source=("https://www.openssl.org/source/${_basename}-${_ver}.tar.gz"
        "https://www.openssl.org/source/${_basename}-${_ver}.tar.gz.asc"
        'ca-dir.patch')
sha256sums=('ddb04774f1e32f0c49751e21b67216ac87852ceb056b75209af2443400636d46'
            'SKIP'
            '0938c8d68110768db4f350a7ec641070686904f2fe7ba630ac94399d7dc8cc5e')
validpgpkeys=('8657ABB260F056B1E5190839D9C4D26D0E604491'
              '7953AC1FBC3DC8B3B292393ED5E9E43F7DF9EE8C')


prepare() {
	cd "$srcdir/$_basename-$_ver"
	# set ca dir to /etc/ssl by default
	patch -p0 -i "$srcdir/ca-dir.patch"
}


build() {

  if [ ${_cpu} == "x86_64" ]; then
    local _platform="linux-x86_64"
    local _optflags='enable-ec_nistp_64_gcc_128'
  elif [ ${_cpu} == "arm" ]; then
    local _platform="linux-generic32"
  else
    local _platform="linux-generic32"
  fi
  cd "${srcdir}/openssl-$_ver"
    mkdir -p "${srcdir}/build-${_target}" && cp -a "${srcdir}/openssl-$_ver/"* "${srcdir}/build-${_target}" && cd "${srcdir}/build-${_target}"
   	# mark stack as non-executable: http://bugs.archlinux.org/task/12434
  	./Configure --prefix=${_prefix}/${_target}/sysroot \
                --cross-compile-prefix=${_target}- \
                --openssldir=/etc/ssl \
                --libdir=lib \
		            shared no-ssl3-method zlib-dynamic ${_optflags} \
		            "${_platform}" \
		            "-Wa,--noexecstack ${CPPFLAGS} ${CFLAGS} ${LDFLAGS}"
    make
}

package() {
    cd "${srcdir}/build-${_target}"
    make DESTDIR="${pkgdir}" MANDIR="${_prefix}/${_target}/sysroot/usr/share/man" MANSUFFIX=ssl install_sw
    install -d "$pkgdir/${_prefix}/${_target}/sysroot/usr/"
    mv "$pkgdir/${_prefix}/${_target}/sysroot/include" "$pkgdir/${_prefix}/${_target}/sysroot/usr/"
    find "$pkgdir/${_prefix}/${_target}" -name '*.a' -name '*.so' -type f -exec ${_target}-strip -g {} \;
}
