# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname="${_target}-libx11"
_basename="${pkgname#${_target}-}"
pkgver=1.6.5
pkgrel=1
pkgdesc="X11 client-side library"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
options=(staticlibs !strip !buildflags)
depends=("${_target}-libxcb" "${_target}-xproto" "${_target}-kbproto")
makedepends=("${_target}-xorg-util-macros" "${_target}-xextproto" "${_target}-xtrans" "${_target}-inputproto" "${_target}-configure")
license=('custom')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2{,.sig})
sha256sums=('4d3890db2ba225ba8c55ca63c6409c1ebb078a2806de59fb16342768ae63435d'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E')
validpgpkeys+=('C41C985FDCF1E5364576638B687393EE37D128F8') # Matthieu Herrb <matthieu.herrb@laas.fr>
validpgpkeys+=('3BB639E56F861FA2E86505690FDD682D974CA72A') # Matt Turner <mattst88@gmail.com>

prepare() {
  cd "${srcdir}/libX11-${pkgver}"
  autoconf
}

build() {
  cd "${srcdir}/libX11-${pkgver}"
  ${_target}-configure --disable-xf86bigfont --disable-malloc0returnsnull .
  make
}

package() {
  cd "${srcdir}/libX11-${pkgver}"
  make DESTDIR="${pkgdir}" install
  find "$pkgdir/${_prefix}/${_target}" -name '*.a' -name '*.so' -type f -exec ${_target}-strip -g {} \;
}
