# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname="${_target}-libxcb"
_basename="${pkgname#${_target}-}"
pkgver=1.13
pkgrel=1
pkgdesc="X11 client-side library"
arch=('x86_64')
url="https://xcb.freedesktop.org/"
options=(staticlibs !strip !buildflags)
depends=("${_target}-xcb-proto>=1.12" "${_target}-libxdmcp" "${_target}-libxau")
makedepends=("${_target}-libxslt" "python" "${_target}-xorg-util-macros" "${_target}-configure")
license=('custom')
source=($url/dist/$_basename-$pkgver.tar.bz2{,.sig}
        libxcb-1.1-no-pthread-stubs.patch)
sha512sums=('d99bbe386a65f60fcc0cc2935e4a365bc0577b5e90433e18778a665a5aee8ebc8e9e8cb0aedac0ac4a276147c0507e759a716874ea4f80792b2fc18eefbc128c'
            'SKIP'
            '19f3a5d740dd21e1060d84c5028ae575c0a69166a125dea62633193a87c387cfd108809dc7cf50b46880286e0830952b9b8d685be4a0303d9bb410eda07647cf')
validpgpkeys=('A66D805F7C9329B4C5D82767CCC4F07FAC641EFF') # "Daniel Stone <daniels@collabora.com>"

prepare() {
  cd "$_basename-$pkgver"
  patch -Np1 -i ../libxcb-1.1-no-pthread-stubs.patch
  autoreconf -vfi
}

build() {
  cd "$_basename-$pkgver"
  ${_target}-configure \
    --enable-xinput \
    --enable-xkb .
  make -j1
}

package() {
  cd "$_basename-$pkgver"
  make DESTDIR="$pkgdir" install
  find "$pkgdir/${_prefix}/${_target}" -name '*.a' -name '*.so' -type f -exec ${_target}-strip -g {} \;
}
