# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname="${_target}-libxslt"
_basename="${pkgname#${_target}-}"
pkgver=1.1.34
pkgrel=1
pkgdesc="XML stylesheet transformation library"
url="http://xmlsoft.org/XSLT/"
arch=(x86_64)
license=(custom)
options=(staticlibs !strip !buildflags)
depends=("${_target}-libxml2" "${_target}-libgcrypt")
makedepends=(git "${_target}-configure")
checkdepends=(docbook-xml)
_commit=3653123f992db24cec417d12600f4c67388025e3  # tags/v1.1.34^0
source=("git+https://gitlab.gnome.org/GNOME/libxslt.git#commit=$_commit"
        0002-Make-generate-id-deterministic.patch)
sha256sums=('SKIP'
            '371f8a27e57f648315d2ef73499e9bdc15bc3d83956c63b98eb6865f18411b62')

pkgver() {
  cd $_basename
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $_basename

  sed -e 's|/usr/bin/python -u|/usr/bin/python2 -u|g' \
      -e 's|/usr/bin/python$|/usr/bin/python2|g' \
      -i python/tests/*.py

  # Make xsltproc output reproducible for packages which use it to generate documentation.
  patch -Np1 -i $srcdir/0002-Make-generate-id-deterministic.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $_basename
  ${_target}-configure .
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $_basename
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -name '*.a' -print -delete
  find "$pkgdir/${_prefix}/${_target}" -name '*.a' -name '*.so' -type f -exec ${_target}-strip -g {} \;
}
