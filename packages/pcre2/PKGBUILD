pkgname=${_target}-pcre2
_basename="${pkgname#${_target}-}"
pkgver=10.34
pkgrel=1
pkgdesc="	A library that implements Perl 5-style regular expressions. 2nd version (${_target})"
arch=(any)
url="http://www.pcre.org/"
license=("BSD")
makedepends=(${_target}-configure ${_target}-readline ${_target}-bzip2 ${_target}-zlib)
depends=("${_target}-gcc")
options=(staticlibs !buildflags)
optdepends=(${_target}-readline ${_target}-bzip2 ${_target}-zlib)

source=("https://ftp.pcre.org/pub/pcre/$_basename-$pkgver.tar.bz2"{,.sig})
sha512sums=('77ad75f8b0b8bbfc2f57932596151bca25b06bd621e0f047e476f38cd127f43e2052460b95c281a7e874aad2b7fd86c8f3413f4a323abb74b9440a42d0ee9524'
            'SKIP')
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8')

build() {
	cd "$srcdir/pcre2-$pkgver"
    mkdir -p build-${_target} && pushd build-${_target}
    ${_target}-configure \
      --enable-jit \
      --enable-pcre2-16 \
      --enable-pcre2-32 \
      --enable-pcre2grep-libz \
      --enable-pcre2grep-libbz2 \
      --enable-pcre2test-libreadline
    make
    popd
}

package() {
    cd "${srcdir}/pcre2-$pkgver/build-${_target}"
    make DESTDIR="$pkgdir" install
}

strip() {
    ${_target}-strip $@
}
export -f strip