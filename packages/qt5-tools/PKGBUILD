# $Id: PKGBUILD 240297 2015-06-03 10:22:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname="${_target}-qt5-tools"
_basename="${pkgname#${_target}-}"
_qtver=5.10.0
pkgver=${_qtver/-/}
pkgrel=1
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework (Development Tools, QtHelp)'
depends=("${_target}-qt5-base" 'hicolor-icon-theme')
options=('staticlibs' '!strip' '!buildflags')
makedepends=("${_target}-qt5-declarative")
optdepends=("${_target}-qt5-doc: documentation" "${_target}-qt5-webkit: for Qt Assistant")
groups=("${_target}-qt" "${_target}-qt5")
conflicts=("${_target}-qtchooser")
_pkgfqn="${_basename/5-/}-everywhere-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('1ff5dc747b7935de85257673424dfdffb231f3409f09a5f833d37e2f625cfe32')

prepare() {
  mkdir -p build
}

build() {
  cd build
  ${_prefix}/bin/${_target}-qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="${pkgdir}" install
  rm ${pkgdir}/${_prefix}/${_target}/bin/qdoc
  cd ../${_pkgfqn}
  # Symlinks for backwards compatibility
  install -d "$pkgdir/${_prefix}/bin"
  for b in "$pkgdir"${_prefix}/${_target}/bin/*; do
    ln -s ${_prefix}/${_target}/bin/$(basename $b) "$pkgdir"${_prefix}/bin/${_target}-$(basename $b)
  done

  find "$pkgdir/${_prefix}/${_target}" -name '*.a' -name '*.so' -type f -exec ${_target}-strip -g {} \;
  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/${_prefix}/${_target}/sysroot/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;
}
