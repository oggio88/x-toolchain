# Maintainer: pingplug <pingplug@foxmail.com>
# Contributor: Philip A Reimer <antreimer@gmail.com>
# Contributor: Schala Zeal <schalaalexiazeal@gmail.com>

pkgname="${_target}-xz"
pkgver=5.2.5
pkgrel=1
pkgdesc="Library and command line tools for XZ and LZMA compressed files (mingw-w64)"
arch=('any')
url="http://tukaani.org/xz"
license=("custom" "GPL" "LGPL")
makedepends=("${_target}-configure")
depends=("${_target}-gcc")
options=(staticlibs !buildflags)
source=("https://tukaani.org/xz/xz-${pkgver}.tar.gz")
md5sums=('0d270c997aff29708c74d53f599ef717')

build() {
    cd "${srcdir}/xz-${pkgver}"
    mkdir -p build-${_target} && pushd build-${_target}
    ${_prefix}/bin/${_target}-configure
    make
    popd
}

package() {
    cd "${srcdir}/xz-${pkgver}/build-${_target}"
    make DESTDIR="$pkgdir" install
}

strip() {
    ${_target}-strip $@
}
export -f strip