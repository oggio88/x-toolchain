# Based on qtbase/mkspecs/devices/linux-beagleboard-g++/qmake.conf from Qt 5.4
#
# Remove EGL dependency/default platform

MAKEFILE_GENERATOR      = UNIX
CONFIG                 += debug_and_release debug_and_release_target precompile_header $${CROSS_COMPILE_CUSTOM_CONFIG}
DEFINES                += UNICODE
QMAKE_INCREMENTAL_STYLE = sublib
QMAKE_COMPILER_DEFINES += __GNUC__
QMAKE_COMPILER          = gcc

include(../common/linux.conf)
include(../common/gcc-base-unix.conf)
include(../common/g++-unix.conf)

load(device_config)

# modifications to g++.conf
QMAKE_CC                = $${CROSS_COMPILE}gcc
QMAKE_CXX               = $${CROSS_COMPILE}g++
QMAKE_LINK              = $${QMAKE_CXX}
QMAKE_LINK_C            = $${QMAKE_CC}
QMAKE_LINK_SHLIB        = $${QMAKE_CXX}
QMAKE_LINK_C_SHLIB      = $${QMAKE_CC}

# modifications to linux.conf
QMAKE_LRELEASE          = $${CROSS_COMPILE}lrelease-qt5
QMAKE_AR                = $${CROSS_COMPILE}ar -rc
QMAKE_OBJCOPY           = $${CROSS_COMPILE}objcopy
QMAKE_NM                = $${CROSS_COMPILE}nm -P
QMAKE_STRIP             = $${CROSS_COMPILE}strip
QMAKE_STRIPFLAGS_LIB   += --strip-unneeded

PKG_CONFIG                  = $${CROSS_COMPILE}pkg-config
QMAKE_PKG_CONFIG            = $${CROSS_COMPILE}pkg-config

#COMPILER_FLAGS          = -march=armv7-a -mtune=cortex-a8 -mfpu=neon -mfloat-abi=softfp
LINK_FLAGS = -lexecinfo
#modifications to gcc-base.conf
QMAKE_CFLAGS           += $${COMPILER_FLAGS}
QMAKE_CXXFLAGS         += $${COMPILER_FLAGS}
QMAKE_CXXFLAGS_RELEASE += -O3

#QMAKE_LIBS             += -lrt -lpthread -ldl

QMAKE_LIBS                  =
QMAKE_LIBS_CORE             = -lz -lpcre2-16
QMAKE_LIBS_GUI_STATIC       = -ljpeg -lpng \
                                $$system($${QMAKE_PKG_CONFIG} --static --libs harfbuzz) \
                                $$system($${QMAKE_PKG_CONFIG} --static --libs freetype2)
QMAKE_LIBS_NETWORK_STATIC   = $$system($${QMAKE_PKG_CONFIG} --static --libs openssl)
#QMAKE_LIBS_COMPAT           =
#QMAKE_LIBS_QT_ENTRY         = -lmingw32 -lqt5main

load(qt_config)
